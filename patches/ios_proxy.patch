# iOS に Proxy を設定する機能を入れるパッチ。
# Objective-C では以下のように利用する。
#
# [factory peerConnectionWithConfiguration:configuration
#                              constraints:constraints
#                      certificateVerifier:certificateVerifier
#                                 delegate:delegate
#                                proxyType:RTCProxyTypeHttps
#                               proxyAgent:@"user-agent"
#                            proxyHostname:@"192.168.100.11"
#                                proxyPort:3456
#                            proxyUsername:@"username"
#                            proxyPassword:@"password"]
#
diff --git a/sdk/objc/api/peerconnection/RTCPeerConnection+Private.h b/sdk/objc/api/peerconnection/RTCPeerConnection+Private.h
index 00f2ef7834..6e0966fe3a 100644
--- a/sdk/objc/api/peerconnection/RTCPeerConnection+Private.h
+++ b/sdk/objc/api/peerconnection/RTCPeerConnection+Private.h
@@ -92,6 +92,19 @@ class PeerConnectionDelegateAdapter : public PeerConnectionObserver {
     certificateVerifier:(nullable id<RTC_OBJC_TYPE(RTCSSLCertificateVerifier)>)certificateVerifier
                delegate:(nullable id<RTC_OBJC_TYPE(RTCPeerConnectionDelegate)>)delegate;
 
+- (nullable instancetype)
+        initWithFactory:(RTC_OBJC_TYPE(RTCPeerConnectionFactory) *)factory
+          configuration:(RTC_OBJC_TYPE(RTCConfiguration) *)configuration
+            constraints:(RTC_OBJC_TYPE(RTCMediaConstraints) *)constraints
+    certificateVerifier:(nullable id<RTC_OBJC_TYPE(RTCSSLCertificateVerifier)>)certificateVerifier
+               delegate:(nullable id<RTC_OBJC_TYPE(RTCPeerConnectionDelegate)>)delegate
+              proxyType:(RTCProxyType)proxyType
+             proxyAgent:(NSString*)proxyAgent
+          proxyHostname:(NSString*)proxyHostname
+              proxyPort:(int)proxyPort
+          proxyUsername:(NSString*)proxyUsername
+          proxyPassword:(NSString*)proxyPassword;
+
 /** Initialize an RTCPeerConnection with a configuration, constraints,
  *  delegate and PeerConnectionDependencies.
  */
diff --git a/sdk/objc/api/peerconnection/RTCPeerConnection.h b/sdk/objc/api/peerconnection/RTCPeerConnection.h
index 466e053492..8f5185d7e4 100644
--- a/sdk/objc/api/peerconnection/RTCPeerConnection.h
+++ b/sdk/objc/api/peerconnection/RTCPeerConnection.h
@@ -82,6 +82,13 @@ typedef NS_ENUM(NSInteger, RTCStatsOutputLevel) {
   RTCStatsOutputLevelDebug,
 };
 
+typedef NS_ENUM(NSInteger, RTCProxyType) {
+  RTCProxyTypeNone,
+  RTCProxyTypeHttps,
+  RTCProxyTypeSocks5,
+  RTCProxyTypeUnknown,
+};
+
 typedef void (^RTCCreateSessionDescriptionCompletionHandler)(
     RTC_OBJC_TYPE(RTCSessionDescription) *_Nullable sdp, NSError *_Nullable error);
 
diff --git a/sdk/objc/api/peerconnection/RTCPeerConnection.mm b/sdk/objc/api/peerconnection/RTCPeerConnection.mm
index f4db472380..45a2b873f2 100644
--- a/sdk/objc/api/peerconnection/RTCPeerConnection.mm
+++ b/sdk/objc/api/peerconnection/RTCPeerConnection.mm
@@ -322,6 +322,36 @@ void OnFailure(RTCError error) override {
 
 }  // namespace webrtc
 
+#include "p2p/client/basic_port_allocator.h"
+#include "pc/rtp_media_utils.h"
+
+namespace webrtc {
+
+class RawCryptString : public rtc::CryptStringImpl {
+ public:
+  RawCryptString(const std::string& str) : str_(str) {}
+  size_t GetLength() const override { return str_.size(); }
+  void CopyTo(char* dest, bool nullterminate) const override {
+    for (int i = 0; i < (int)str_.size(); i++) {
+      *dest++ = str_[i];
+    }
+    if (nullterminate) {
+      *dest = '\0';
+    }
+  }
+  std::string UrlEncode() const override { return ""; }
+  CryptStringImpl* Copy() const override { return new RawCryptString(str_); }
+  void CopyRawTo(std::vector<unsigned char>* dest) const override {
+    dest->assign(str_.begin(), str_.end());
+  }
+
+ private:
+  std::string str_;
+};
+
+}  // namespace webrtc
+
+
 @implementation RTC_OBJC_TYPE (RTCPeerConnection) {
   RTC_OBJC_TYPE(RTCPeerConnectionFactory) * _factory;
   NSMutableArray<RTC_OBJC_TYPE(RTCMediaStream) *> *_localStreams;
@@ -353,6 +383,53 @@ - (nullable instancetype)initWithFactory:(RTC_OBJC_TYPE(RTCPeerConnectionFactory
                            delegate:delegate];
 }
 
+- (nullable instancetype)initWithFactory:(RTC_OBJC_TYPE(RTCPeerConnectionFactory) *)factory
+                           configuration:(RTC_OBJC_TYPE(RTCConfiguration) *)configuration
+                             constraints:(RTC_OBJC_TYPE(RTCMediaConstraints) *)constraints
+                     certificateVerifier:
+                         (nullable id<RTC_OBJC_TYPE(RTCSSLCertificateVerifier)>)certificateVerifier
+                                delegate:(id<RTC_OBJC_TYPE(RTCPeerConnectionDelegate)>)delegate
+                               proxyType:(RTCProxyType)proxyType
+                              proxyAgent:(NSString*)proxyAgent
+                           proxyHostname:(NSString*)proxyHostname
+                               proxyPort:(int)proxyPort
+                           proxyUsername:(NSString*)proxyUsername
+                           proxyPassword:(NSString*)proxyPassword {
+  NSParameterAssert(factory);
+  std::unique_ptr<webrtc::PeerConnectionDependencies> dependencies =
+      std::make_unique<webrtc::PeerConnectionDependencies>(nullptr);
+  if (certificateVerifier != nil) {
+    dependencies->tls_cert_verifier = webrtc::ObjCToNativeCertificateVerifier(certificateVerifier);
+  }
+  if (proxyType != RTCProxyTypeNone) {
+    std::unique_ptr<webrtc::PeerConnectionInterface::RTCConfiguration> config(
+        [configuration createNativeConfiguration]);
+    auto context = factory.connectionContext;
+    dependencies->allocator.reset(new cricket::BasicPortAllocator(
+        context->default_network_manager(), context->default_socket_factory(),
+        config->turn_customizer));
+    dependencies->allocator->SetPortRange(
+        config->port_allocator_config.min_port,
+        config->port_allocator_config.max_port);
+    dependencies->allocator->set_flags(config->port_allocator_config.flags);
+
+    rtc::ProxyInfo pi;
+    pi.type =
+        proxyType == RTCProxyTypeNone ? rtc::PROXY_NONE :
+        proxyType == RTCProxyTypeHttps ? rtc::PROXY_HTTPS :
+        proxyType == RTCProxyTypeSocks5 ? rtc::PROXY_SOCKS5 : rtc::PROXY_UNKNOWN;
+    pi.address = rtc::SocketAddress([proxyHostname UTF8String], proxyPort);
+    pi.username = [proxyUsername UTF8String];
+    pi.password = rtc::CryptString(webrtc::RawCryptString([proxyPassword UTF8String]));
+    dependencies->allocator->set_proxy([proxyAgent UTF8String], pi);
+  }
+  return [self initWithDependencies:factory
+                      configuration:configuration
+                        constraints:constraints
+                       dependencies:std::move(dependencies)
+                           delegate:delegate];
+}
+
 - (nullable instancetype)
     initWithDependencies:(RTC_OBJC_TYPE(RTCPeerConnectionFactory) *)factory
            configuration:(RTC_OBJC_TYPE(RTCConfiguration) *)configuration
diff --git a/sdk/objc/api/peerconnection/RTCPeerConnectionFactory+Private.h b/sdk/objc/api/peerconnection/RTCPeerConnectionFactory+Private.h
index 9613646270..b7f3b778b9 100644
--- a/sdk/objc/api/peerconnection/RTCPeerConnectionFactory+Private.h
+++ b/sdk/objc/api/peerconnection/RTCPeerConnectionFactory+Private.h
@@ -13,6 +13,7 @@
 #include "api/peer_connection_interface.h"
 #include "api/scoped_refptr.h"
 #include "rtc_base/thread.h"
+#include "pc/connection_context.h"
 
 NS_ASSUME_NONNULL_BEGIN
 
@@ -30,6 +31,7 @@ NS_ASSUME_NONNULL_BEGIN
 @property(nonatomic, readonly) rtc::Thread* signalingThread;
 @property(nonatomic, readonly) rtc::Thread* workerThread;
 
+    @property(nonatomic, readonly) rtc::scoped_refptr<webrtc::ConnectionContext> connectionContext;
 @end
 
 NS_ASSUME_NONNULL_END
diff --git a/sdk/objc/api/peerconnection/RTCPeerConnectionFactory.h b/sdk/objc/api/peerconnection/RTCPeerConnectionFactory.h
index 88aac990f2..feca021d9a 100644
--- a/sdk/objc/api/peerconnection/RTCPeerConnectionFactory.h
+++ b/sdk/objc/api/peerconnection/RTCPeerConnectionFactory.h
@@ -32,6 +32,8 @@ NS_ASSUME_NONNULL_BEGIN
 @protocol RTC_OBJC_TYPE
 (RTCSSLCertificateVerifier);
 
+typedef NS_ENUM(NSInteger, RTCProxyType);
+
 RTC_OBJC_EXPORT
 @interface RTC_OBJC_TYPE (RTCPeerConnectionFactory) : NSObject
 
@@ -91,6 +93,19 @@ RTC_OBJC_EXPORT
                     (id<RTC_OBJC_TYPE(RTCSSLCertificateVerifier)>)certificateVerifier
                            delegate:(nullable id<RTC_OBJC_TYPE(RTCPeerConnectionDelegate)>)delegate;
 
+- (nullable RTC_OBJC_TYPE(RTCPeerConnection) *)
+    peerConnectionWithConfiguration:(RTC_OBJC_TYPE(RTCConfiguration) *)configuration
+                        constraints:(RTC_OBJC_TYPE(RTCMediaConstraints) *)constraints
+                certificateVerifier:
+                    (nullable id<RTC_OBJC_TYPE(RTCSSLCertificateVerifier)>)certificateVerifier
+                           delegate:(nullable id<RTC_OBJC_TYPE(RTCPeerConnectionDelegate)>)delegate
+                          proxyType:(RTCProxyType)proxyType
+                         proxyAgent:(NSString*)proxyAgent
+                      proxyHostname:(NSString*)proxyHostname
+                          proxyPort:(int)proxyPort
+                      proxyUsername:(NSString*)proxyUsername
+                      proxyPassword:(NSString*)proxyPassword;
+
 /** Set the options to be used for subsequently created RTCPeerConnections */
 - (void)setOptions:(nonnull RTC_OBJC_TYPE(RTCPeerConnectionFactoryOptions) *)options;
 
diff --git a/sdk/objc/api/peerconnection/RTCPeerConnectionFactory.mm b/sdk/objc/api/peerconnection/RTCPeerConnectionFactory.mm
index c4d89e911d..a4824cb88b 100644
--- a/sdk/objc/api/peerconnection/RTCPeerConnectionFactory.mm
+++ b/sdk/objc/api/peerconnection/RTCPeerConnectionFactory.mm
@@ -51,6 +51,67 @@
 #import "sdk/objc/native/api/audio_device_module.h"
 #endif
 
+#include <pc/peer_connection_factory.h>
+#include <pc/peer_connection_factory_proxy.h>
+#include <api/peer_connection_interface.h>
+#include <api/scoped_refptr.h>
+#include <pc/connection_context.h>
+
+namespace webrtc {
+
+// webrtc::PeerConnectionFactory から ConnectionContext を取り出す方法が無いので、
+// 継承して無理やり使えるようにする
+class PeerConnectionFactoryWithContext : public webrtc::PeerConnectionFactory {
+ public:
+  PeerConnectionFactoryWithContext(
+      webrtc::PeerConnectionFactoryDependencies dependencies)
+      : PeerConnectionFactoryWithContext(
+            webrtc::ConnectionContext::Create(&dependencies),
+            &dependencies) {}
+  PeerConnectionFactoryWithContext(
+      rtc::scoped_refptr<webrtc::ConnectionContext> context,
+      webrtc::PeerConnectionFactoryDependencies* dependencies)
+      : webrtc::PeerConnectionFactory(context, dependencies),
+        conn_context_(context) {}
+
+  static rtc::scoped_refptr<PeerConnectionFactoryWithContext> Create(
+      webrtc::PeerConnectionFactoryDependencies dependencies) {
+    return rtc::make_ref_counted<PeerConnectionFactoryWithContext>(
+        std::move(dependencies));
+  }
+
+  rtc::scoped_refptr<webrtc::ConnectionContext> GetContext() const {
+    return conn_context_;
+  }
+
+private:
+  rtc::scoped_refptr<webrtc::ConnectionContext> conn_context_;
+};
+
+rtc::scoped_refptr<webrtc::PeerConnectionFactoryInterface>
+CreateModularPeerConnectionFactoryWithContext(
+    webrtc::PeerConnectionFactoryDependencies dependencies,
+    rtc::scoped_refptr<webrtc::ConnectionContext>& context) {
+  using result_type =
+      std::pair<rtc::scoped_refptr<webrtc::PeerConnectionFactoryInterface>,
+                rtc::scoped_refptr<webrtc::ConnectionContext>>;
+  auto p = dependencies.signaling_thread->BlockingCall([&dependencies]() {
+        auto factory =
+            PeerConnectionFactoryWithContext::Create(std::move(dependencies));
+        if (factory == nullptr) {
+          return result_type(nullptr, nullptr);
+        }
+        auto context = factory->GetContext();
+        auto proxy = webrtc::PeerConnectionFactoryProxy::Create(
+            factory->signaling_thread(), factory->worker_thread(), factory);
+        return result_type(proxy, context);
+      });
+  context = p.second;
+  return p.first;
+}
+
+} // namespace webrtc
+
 @implementation RTC_OBJC_TYPE (RTCPeerConnectionFactory) {
   std::unique_ptr<rtc::Thread> _networkThread;
   std::unique_ptr<rtc::Thread> _workerThread;
@@ -59,6 +120,7 @@ @implementation RTC_OBJC_TYPE (RTCPeerConnectionFactory) {
 }
 
 @synthesize nativeFactory = _nativeFactory;
+@synthesize connectionContext = _connectionContext;
 
 - (rtc::scoped_refptr<webrtc::AudioDeviceModule>)audioDeviceModule {
 #if defined(WEBRTC_IOS)
@@ -145,7 +207,7 @@ - (instancetype)initWithNoMedia {
     if (webrtc::field_trial::IsEnabled("WebRTC-Network-UseNWPathMonitor")) {
       dependencies.network_monitor_factory = webrtc::CreateNetworkMonitorFactory();
     }
-    _nativeFactory = webrtc::CreateModularPeerConnectionFactory(std::move(dependencies));
+    _nativeFactory = webrtc::CreateModularPeerConnectionFactoryWithContext(std::move(dependencies), _connectionContext);
     NSAssert(_nativeFactory, @"Failed to initialize PeerConnectionFactory!");
   }
   return self;
@@ -213,7 +275,7 @@ - (instancetype)initWithNativeAudioEncoderFactory:
     dependencies.event_log_factory =
         std::make_unique<webrtc::RtcEventLogFactory>(dependencies.task_queue_factory.get());
     dependencies.network_controller_factory = std::move(networkControllerFactory);
-    _nativeFactory = webrtc::CreateModularPeerConnectionFactory(std::move(dependencies));
+    _nativeFactory = webrtc::CreateModularPeerConnectionFactoryWithContext(std::move(dependencies), _connectionContext);
     NSAssert(_nativeFactory, @"Failed to initialize PeerConnectionFactory!");
   }
   return self;
@@ -265,6 +327,31 @@ - (instancetype)initWithNativeAudioEncoderFactory:
   return [[RTC_OBJC_TYPE(RTCMediaStream) alloc] initWithFactory:self streamId:streamId];
 }
 
+- (nullable RTC_OBJC_TYPE(RTCPeerConnection) *)
+    peerConnectionWithConfiguration:(RTC_OBJC_TYPE(RTCConfiguration) *)configuration
+                        constraints:(RTC_OBJC_TYPE(RTCMediaConstraints) *)constraints
+                certificateVerifier:
+                    (nullable id<RTC_OBJC_TYPE(RTCSSLCertificateVerifier)>)certificateVerifier
+                           delegate:(nullable id<RTC_OBJC_TYPE(RTCPeerConnectionDelegate)>)delegate
+                          proxyType:(RTCProxyType)proxyType
+                         proxyAgent:(NSString*)proxyAgent
+                      proxyHostname:(NSString*)proxyHostname
+                          proxyPort:(int)proxyPort
+                      proxyUsername:(NSString*)proxyUsername
+                      proxyPassword:(NSString*)proxyPassword {
+  return [[RTC_OBJC_TYPE(RTCPeerConnection) alloc] initWithFactory:self
+                                                     configuration:configuration
+                                                       constraints:constraints
+                                               certificateVerifier:certificateVerifier
+                                                          delegate:delegate
+                                                         proxyType:proxyType
+                                                        proxyAgent:proxyAgent
+                                                     proxyHostname:proxyHostname
+                                                         proxyPort:proxyPort
+                                                     proxyUsername:proxyUsername
+                                                     proxyPassword:proxyPassword];
+}
+
 - (nullable RTC_OBJC_TYPE(RTCPeerConnection) *)
     peerConnectionWithConfiguration:(RTC_OBJC_TYPE(RTCConfiguration) *)configuration
                         constraints:(RTC_OBJC_TYPE(RTCMediaConstraints) *)constraints